REBOL []
( ; setup
  demo: func [x /only t: b:] [
    t: load-rem x
    rem
    p i "ReM code:"
    div .code mold/only x
    p i "LOAD-REM ReM => DoT:"
    div .code mold t
    p i "MOLD-HTML Dot => HTML:"
    div .code mold-html t
    p i "Display:"
    div .sample t
    br
  ]

  note: func [x] [
    rem p [span background: "yellow" b "NOTE" space x]
  ]

  tit: "ReM format tutorial"
) ; end setup

doc [
head [
  viewport 1
  title tit
  style [
    body  font-size: "12pt"
    h1  text-align: center
    div padding: "1ex"
    .sample  border: "2px solid gray"
    .code  background: #dddddd
      font-family: monospace
      font-size: "11pt"
      white-space: pre-wrap
  ]
]
body [

h1 tit

h2 ["ReM code"]

p {ReM code is processed by LOAD-REM, that convert it into a document tree (DoT). MOLD-HTML reads that tree and outputs HTML text.}

h2 "Text"

demo ["Text must be quoted; <, &, >, are ok."]
demo [{Space must be explicitly inserted}
  { in strings } {or with 'space. } space {This} {is} {one} {long} {word.}
]

h2 "Tags"

p  "Tags are words; content is a string or a block."

demo [
  h1 "Title"
  p [{paragraph with breakline} br
    b {bold} { normal } i {italic}
  ]
]

note " tag names are those in HTML, but <HTML> itself is substituded by DOC."

p {After tag name you can add attributes:}

h2 {Generic attributes}

p [{Generic attributes are in the form } i {/refinement value} { (value is evaluated)}]

demo [p /align first["right" "left"] [
  {first line} br {second line}
] ]

h3 "Styles"

p [{Style properties are in the form } i {set-word: value} { (value is evaluated.)}]

demo [span font-size: to-percent 7 / 4 color: 'red "Great red text"]

p {Style tag has its own syntax -- see next example.}

h3 "Ids, classes and STYLE tag"

p [{Ids are in the form } i "#id" {, classes are } i ".class"]

demo [
  style [
    #bluey .bluey color: "blue" background: "yellow"
    .big font-size: 200%
  ]
  span #bluey "Blue on yellow"
  br
  span .bluey .big "Big blue on yellow"
]

h2 "HREF and SRC"

p "URLs and FILES are HREF and SRC attributes:"

demo [
  a https://github.com/metaeducation/ren-c
  img http://giuliolunati.altervista.org/r3/ren-c-logo.png height: "3.5ex" space
  a %/ font-size: "200%" "ROOT"
]

h2 "Define things -- Groups and REM function"

p "Code contained in a GROUP is evaluated but produces no output. Handy to define things:"

demo [(T: "tora ") p [T T T]]

p ["But remember: code in GROUP isn't processed by LOAD-REM, only evaluated -- so, if you want more than a simple string, you need REM function:"]

demo [(T: "tora " TI: rem [i T] TB: rem b T) p [T TI TB]]

note "REM function acts as LOAD-REM, but inside ReM code. It accepts a BLOCK or a VARARGS."

p "You can also define functions:"

demo [(
  bi: func [x] [rem b i x]
  b+i: func [x y] [rem b x "+" i y]
  ) bi "bold italic" space  b+i "bold" "italic"
]

] ;body
] ;doc
;; vim: set syn=rebol sw=2 ts=2 sts=2 expandtab:
